
// @strict-types

#Область ОбработчикиСобытий

// Параметры:
//  ПараметрКоманды - СправочникСсылка.Спец_ВнешниеОбработки
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если Не ЗначениеЗаполнено(ПараметрКоманды) Тогда
		ВызватьИсключение "Не указан элемент для записи!";
	КонецЕсли;
	
	// ++ Спец_БСП.Версия1С_15_Ниже
	////ВызватьИсключение "Недоступно!";
	// -- Спец_БСП.Версия1С_15_Ниже
	
	// ++ Спец_БСП.Версия1С_15_ВышеИлиРавно
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаКоманды_ПослеЗагрузкиФайла", ЭтотОбъект, ПараметрКоманды);
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов("Выберите файл для записи во внешнюю обработку",
			Ложь,
			СтрШаблон("Внешние отчеты и обработки (*.%1, *.%2)|*.%1;*.%2|Внешние отчеты (*.%1)|*.%1|Внешние обработки (*.%2)|*.%2", "erf", "epf"));
	
	Спец_ОбщегоНазначенияКлиент.ЗагрузитьФайлыНаСервер(ПараметрыДиалога, ОписаниеОповещения);
	// -- Спец_БСП.Версия1С_15_ВышеИлиРавно
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// ++ Спец_БСП.Версия1С_15_ВышеИлиРавно

// Параметры:
//  ПомещенныйФайл - ОписаниеПомещенногоФайла, Неопределено -
//  СправочникСсылка - СправочникСсылка.Спец_ВнешниеОбработки 
&НаКлиенте
Процедура ОбработкаКоманды_ПослеЗагрузкиФайла(Знач ПомещенныйФайл, Знач СправочникСсылка) Экспорт
	
	Если ПомещенныйФайл = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбработкаКоманды_ЗаписатьИзмененияНаСервере(ПомещенныйФайл.Адрес, СправочникСсылка);
	Спец_ОбщегоНазначенияКлиент.СообщитьПользователю("Успешно");
	
КонецПроцедуры

// Параметры:
//  Адрес - Строка
//  СправочникСсылка - СправочникСсылка.Спец_ВнешниеОбработки
&НаСервере
Процедура ОбработкаКоманды_ЗаписатьИзмененияНаСервере(Знач Адрес, Знач СправочникСсылка)
	
	СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
	СправочникОбъект.ХранилищеВнешнейОбработки = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Адрес));
	
	СправочникОбъект.Записать();
	
КонецПроцедуры

// -- Спец_БСП.Версия1С_15_ВышеИлиРавно

#КонецОбласти
