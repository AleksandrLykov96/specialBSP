// @strict-types

#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	// ++ Спец_БСП.ОбменСДругимиБазами_ЧтоТоЕсть
	МодульОбменСДругимиБазами = Спец_ОбщегоНазначенияКлиентСервер.ОбщийМодуль("Спец_ОбменСДругимиБазамиКлиентСервер");
	Если МодульОбменСДругимиБазами = Неопределено Тогда
		ВызватьИсключение "Не установлена подсистема обменов с другими базами!";
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаКоманды_ПослеВыбораТипаПинга", ЭтотОбъект, ПараметрКоманды);
	
	СписокКнопок = Новый СписокЗначений; // СписокЗначений из КодВозвратаДиалога
	СписокКнопок.Добавить(КодВозвратаДиалога.Да, "Через локальный адрес");
	СписокКнопок.Добавить(КодВозвратаДиалога.Нет, "Через внешний адрес");
	СписокКнопок.Добавить(КодВозвратаДиалога.Отмена);
	
	//@skip-check invocation-parameter-type-intersect
	ПоказатьВопрос(ОписаниеОповещения,
			"Выберите через что пинговать",
			СписокКнопок,
			30,
			КодВозвратаДиалога.Да,
			"Тип пинга",
			КодВозвратаДиалога.Да);
	// -- Спец_БСП.ОбменСДругимиБазами_ЧтоТоЕсть
	
КонецПроцедуры

#КонецОбласти

// ++ Спец_БСП.ОбменСДругимиБазами_ЧтоТоЕсть
#Область СлужебныеПроцедурыИФункции

// Параметры:
//  Результат - КодВозвратаДиалога
//  БазаДляПроверки - СправочникСсылка.Спец_Базы
&НаКлиенте
Процедура ОбработкаКоманды_ПослеВыбораТипаПинга(Знач Результат, Знач БазаДляПроверки) Экспорт
	
	Если Результат = Неопределено Или Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаКоманды_ПослеПинга", ЭтотОбъект, БазаДляПроверки);
	Спец_ОбменСДругимиБазамиКлиентСервер.ПропинговатьБазу(БазаДляПроверки, Результат = КодВозвратаДиалога.Да, Истина, , , ОписаниеОповещения);
	
КонецПроцедуры

// Параметры:
//  Результат - Булево
//  БазаДляПроверки - СправочникСсылка.Спец_Базы
&НаКлиенте
Процедура ОбработкаКоманды_ПослеПинга(Знач Результат, Знач БазаДляПроверки) Экспорт
	
	Если Результат <> Истина Тогда
		Спец_ОбщегоНазначенияКлиент.СообщитьПользователю("Не удалось пропинговать базу! Проверьте настройки подключения", БазаДляПроверки);
	Иначе
		Спец_ОбщегоНазначенияКлиент.СообщитьПользователю("Успешно", БазаДляПроверки);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
// -- Спец_БСП.ОбменСДругимиБазами_ЧтоТоЕсть
