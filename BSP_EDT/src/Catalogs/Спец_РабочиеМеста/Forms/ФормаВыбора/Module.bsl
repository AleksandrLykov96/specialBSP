
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ТекущееРабочееМесто = Спец_ОбщегоНазначенияКлиентСерверПовтИсп.ЗначениеПараметраСеанса("Спец_РабочееМестоКлиента");
	Если Не Спец_ОбщегоНазначенияВызовСервера.ЭтоПустаяИлиБитаяСсылка(ТекущееРабочееМесто) Тогда
		
		Отказ = Истина;
		
		ТекстВопроса = "Создание нового рабочего места не требуется. 
				|Для данного идентификатора клиента оно уже создано.
				|Открыть существующее рабочее место?";
		
		ОписаниеОповещения = Новый ОписаниеОповещения("СписокПередНачаломДобавленияПослеУточнения", ЭтотОбъект, ТекущееРабочееМесто);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 60, КодВозвратаДиалога.Нет, "Вы уверены");
		
	КонецЕсли;
	
КонецПроцедуры 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// Выделяем текущее рабочее место
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	Элемент.Поля.Элементы.Добавить().Поле = Новый ПолеКомпоновкиДанных(Элементы.Список.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Список.Ссылка");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = ПараметрыСеанса.Спец_РабочееМестоКлиента;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Шрифт", ШрифтыСтиля.Спец_ШрифтВыделениеЖирным);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавленияПослеУточнения(Знач Результат, Знач Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да И ЗначениеЗаполнено(Параметры) Тогда
		ПоказатьЗначение(, Параметры);
	КонецЕсли;  
	
КонецПроцедуры

#КонецОбласти
