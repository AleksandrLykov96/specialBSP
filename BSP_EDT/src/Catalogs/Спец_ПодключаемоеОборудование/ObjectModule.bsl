
// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ВыборкаРезультат = ПолучитьВыборкуПоУстановленнымДрайверам();
	Если ВыборкаРезультат.Следующий() Тогда
		
		ТекстСообщения = СтрШаблон("По драйверу ""%1"" на рабочее место ""%2"" уже есть установленное оборудование - ""%3""!",
				ДрайверОборудования,
				РабочееМесто,
				ВыборкаРезультат.Ссылка);
		
		Спец_ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , "Объект.ДрайверОборудования", , Отказ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	УстройствоИспользуется = Ложь;
	ДополнительныеПараметры.Очистить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращаемое значение:
//  ВыборкаИзРезультатаЗапроса:
//		* Ссылка - СправочникСсылка.Спец_ПодключаемоеОборудование
Функция ПолучитьВыборкуПоУстановленнымДрайверам()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Спец_ПодключаемоеОборудование.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.Спец_ПодключаемоеОборудование КАК Спец_ПодключаемоеОборудование
	|ГДЕ
	|	Спец_ПодключаемоеОборудование.ДрайверОборудования = &ДрайверОборудования
	|	И Спец_ПодключаемоеОборудование.РабочееМесто = &РабочееМесто
	|	И Спец_ПодключаемоеОборудование.Ссылка <> &Ссылка
	|	И НЕ Спец_ПодключаемоеОборудование.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("ДрайверОборудования", ДрайверОборудования);
	Запрос.УстановитьПараметр("РабочееМесто", РабочееМесто);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Возврат Запрос.Выполнить().Выбрать();
	
КонецФункции

#КонецОбласти

#Иначе

	ВызватьИсключение "Недопустимый вызов объекта на клиенте."

#КонецЕсли
