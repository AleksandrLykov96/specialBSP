
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда     
		Возврат;
	КонецЕсли;
	
	Спец_РаботаСФормами.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Спец_РаботаСФормамиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ОбновитьИнформациюДрайвераОборудования();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Спец_РаботаСФормамиКлиент.ПриЗакрытии(ЭтотОбъект, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаУстановитьДрайвер(Команда)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ВызватьИсключение "Необходимо записать объект перед установкой драйвера!";
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПереустановкиДрайвера", ЭтотОбъект);
	Спец_МенеджерОборудованияКлиент.НачатьПереустановкуДрайвера(Объект.Ссылка, ОписаниеОповещения);
	
КонецПроцедуры

// ++ Спец_БСП.Спец_ЛогированиеОбъектов
&НаКлиенте
Процедура Подключаемый_ОткрытьЛогиПоОбъекту(Команда)
	
	Спец_ЛогированиеОбъектовКлиент.ОткрытьЛогиПоОбъекту(ЭтотОбъект);
	
КонецПроцедуры
// -- Спец_БСП.Спец_ЛогированиеОбъектов

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьИнформациюДрайвераОборудования()
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьИнформациюДрайвераОборудования_Завершение", ЭтотОбъект);
	Спец_МенеджерОборудованияКлиент.НачатьПолучениеСтруктурыОписанияДрайвера(ЭтотОбъект, Объект.Ссылка, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюДрайвераОборудования_Завершение(Знач РезультатВыполнения, Знач ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВыполнения.Успешно Тогда
		
		Спец_ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатВыполнения.ТекстОшибки, , "ТекущийСтатус", "ТекущийСтатус", , УникальныйИдентификатор);
		ТекущийСтатус = "Не удалось получить данные драйвера!";
		
	Иначе
		
		//@skip-check assign-form-attribute
		ТекущийСтатус = Спец_СтроковыеФункцииКлиентСервер.СформироватьТекстовоеПредставлениеОбъекта(РезультатВыполнения.Результат);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПереустановкиДрайвера(Знач РезультатВыполнения, Знач ДополнительныеПараметры) Экспорт
	
	Если Не РезультатВыполнения.Успешно Тогда
		Спец_ОбщегоНазначенияКлиент.СообщитьПользователю(РезультатВыполнения.ТекстОшибки, , "ТекущийСтатус", "ТекущийСтатус", , УникальныйИдентификатор);
	КонецЕсли;
	
	ОбновитьИнформациюДрайвераОборудования();
	
КонецПроцедуры

#КонецОбласти
