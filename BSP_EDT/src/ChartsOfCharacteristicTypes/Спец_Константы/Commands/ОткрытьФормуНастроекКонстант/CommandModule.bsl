// @strict-types

#Область ОбработчикиСобытий

// Параметры:
//  ПараметрКоманды - ПланВидовХарактеристикСсылка.Спец_Константы
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыОткрытияФормы = Новый Структура;
	Если ЗначениеЗаполнено(ПараметрКоманды) Тогда
		
		ЭтоГруппа = Спец_ОбщегоНазначенияВызовСервера.ЗначениеРеквизитаОбъекта(ПараметрКоманды, "ЭтоГруппа");
		Если Не ЭтоГруппа Тогда
			ПараметрыОткрытияФормы.Вставить("КонстантаДляНастройки", ПараметрКоманды);
		КонецЕсли;
		
	КонецЕсли;
	
	ОписаниеОповещения = Неопределено;
	
	// ++ Спец_БСП.Версия1С_14_ВышеИлиРавно
	Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("ФормаКлиентскогоПриложения")
		И (ПараметрыВыполненияКоманды.Источник.ИмяФормы = "ПланВидовХарактеристик.Спец_Константы.Форма.ФормаСписка"
			Или ПараметрыВыполненияКоманды.Источник.ИмяФормы = "ПланВидовХарактеристик.Спец_Константы.Форма.ФормаВыбора") Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаКоманды_ПослеЗакрытияФормы", ЭтотОбъект);
		
	КонецЕсли;
	// -- Спец_БСП.Версия1С_14_ВышеИлиРавно
	
	// ++ Спец_БСП.Версия1С_14_Ниже
	////Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("УправляемаяФорма")
	////	И (ПараметрыВыполненияКоманды.Источник.ИмяФормы = "ПланВидовХарактеристик.Спец_Константы.ФормаНастройкиНочногоРасчета.ФормаСписка"
	////		Или ПараметрыВыполненияКоманды.Источник.ИмяФормы = "ПланВидовХарактеристик.Спец_Константы.ФормаНастройкиНочногоРасчета.ФормаВыбора") Тогда
	////	
	////	ДополнительныеПараметры = Новый Структура("СписокДляОбновления", ПараметрыВыполненияКоманды.Источник.Элементы.Список);
	////	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаКоманды_ПослеЗакрытияФормы", ЭтотОбъект, ДополнительныеПараметры);
	////	
	////КонецЕсли;
	// -- Спец_БСП.Версия1С_14_Ниже
	
	//@skip-check invocation-parameter-type-intersect
	ОткрытьФорму("ПланВидовХарактеристик.Спец_Константы.Форма.ФормаНастроекКонстант",
			ПараметрыОткрытияФормы,
			ПараметрыВыполненияКоманды.Источник,
			ПараметрыВыполненияКоманды.Уникальность,
			ПараметрыВыполненияКоманды.Окно,
			ПараметрыВыполненияКоманды.НавигационнаяСсылка,
			ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  Результат - Произвольный
//  ДополнительныеПараметры - Неопределено
&НаКлиенте
Процедура ОбработкаКоманды_ПослеЗакрытияФормы(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	ОповеститьОбИзменении(Тип("ПланВидовХарактеристикСсылка.Спец_Константы"));
	
КонецПроцедуры

#КонецОбласти
