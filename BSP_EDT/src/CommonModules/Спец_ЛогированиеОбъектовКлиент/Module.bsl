// @strict-types

#Область ПрограммныйИнтерфейс

// Открыть логи по объекту.
// 
// Параметры:
//  Форма - см. Спец_РаботаСФормами.ПриСозданииНаСервере.Форма
//
Процедура ОткрытьЛогиПоОбъекту(Форма) Экспорт
	
	ЭтоУправляемаяФорма = Спец_ОбщегоНазначенияКлиентСервер.ЭтоУправляемаяФорма(Форма);
	Если Не Спец_ЛогированиеОбъектовКлиентСервер.ЛогиДоступныПоФорме(Форма, ЭтоУправляемаяФорма) Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоСсылочныйОбъект = ?(ЭтоУправляемаяФорма, Форма.Спец_ЭтоСсылочныйОбъект, Форма.ЭлементыФормы.Спец_ЭтоСсылочныйОбъект.Значение);
	Если ЭтоСсылочныйОбъект Тогда
		
		СтруктураКлючаДляОткрытия = Новый Структура("Данные", ?(ЭтоУправляемаяФорма, Форма.Объект.Ссылка, Форма.Ссылка));
		
	Иначе
		
		ПолноеИмяМетаданных = ?(ЭтоУправляемаяФорма, Форма.Спец_ПолноеИмяМетаданных, Форма.ЭлементыФормы.Спец_ПолноеИмяМетаданных.Значение);
		ПараметрыКлюча = Новый Структура();
		
		#Если ТонкийКлиент Или ВебКлиент Или ТолстыйКлиентУправляемоеПриложение Тогда
			МассивИзмерений = Спец_ОбщегоНазначенияВызовСервера.ПолучитьМассивИменРеквизитовДляУникальностиЗаписиРегистра(ПолноеИмяМетаданных);
		#Иначе
			МассивИзмерений = Спец_ОбщегоНазначения.ПолучитьМассивИменРеквизитовДляУникальностиЗаписиРегистра(ПолноеИмяМетаданных);
		#КонецЕсли
		
		ЗначенияДляЗаполнения = ?(ЭтоУправляемаяФорма, Форма.Запись, Форма);
		Для Каждого Измерение Из МассивИзмерений Цикл
			ПараметрыКлюча.Вставить(Измерение, ЗначенияДляЗаполнения[Измерение]);
		КонецЦикла;
		
		#Если ТонкийКлиент Или ВебКлиент Или ТолстыйКлиентУправляемоеПриложение Тогда
			КлючЗаписи = Спец_ОбщегоНазначенияВызовСервера.ПолучитьКлючЗаписиРегистраПоСтруктуреЗначений(ПолноеИмяМетаданных, ПараметрыКлюча);
		#Иначе
			КлючЗаписи = Спец_ОбщегоНазначения.ПолучитьКлючЗаписиРегистраПоСтруктуреЗначений(ПолноеИмяМетаданных, ПараметрыКлюча);
		#КонецЕсли
		
		СтруктураКлючаДляОткрытия = Новый Структура("Данные", КлючЗаписи);
		
	КонецЕсли;
	
	//@skip-check wrong-string-literal-content
	ОткрытьФорму("ФормаИсторииИзмененийИсторииДанных", СтруктураКлючаДляОткрытия);
	
КонецПроцедуры

#КонецОбласти
