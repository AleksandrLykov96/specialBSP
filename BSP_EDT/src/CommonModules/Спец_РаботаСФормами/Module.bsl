
#Область ПрограммныйИнтерфейс

// Обработчик события формы "ПриСозданииНаСервере". Вызывается только с управляемых форм.
// 
// Параметры:
//	 Форма - ФормаКлиентскогоПриложения - форма для добавления кнопки:
//  		* Команды - КомандыФормы
//  		* Элементы - ВсеЭлементыФормы
//  		* КоманднаяПанель - ГруппаФормы
//  		* Спец_ЭтоСсылочныйОбъект - Булево
//  		* Спец_ЭтоРегистр - Булево
//  		* Спец_ПолноеИмяМетаданных - Строка
//  		* Объект - см. Спец_ОбщегоНазначения.ЗаписатьОбъект.ОбъектДляЗаписи
//  		* Запись - см. Спец_ОбщегоНазначения.ЗаписатьОбъект.ОбъектДляЗаписи
//  		* Спец_ИспользоватьПодключаемоеОборудование - Булево
//  	  - Форма - Обычная форма (EDT не умеет с обычными формами):
//  		* ЭлементыФормы - Структура:
//  			** Спец_ЭтоСсылочныйОбъект - Структура:
//  				*** Значение - Булево
//  			** Спец_ЭтоРегистр - Структура:
//  				*** Значение - Булево
//  			** Спец_ПолноеИмяМетаданных - Структура:
//  				*** Значение - Строка
//  		* Ссылка - ЛюбаяСсылка
//  		* Спец_ИспользоватьПодключаемоеОборудование - Булево
//  		* РегистрСведенийМенеджерЗаписи - РегистрСведенийМенеджерЗаписи
//  Отказ - Булево
//  СтандартнаяОбработка - Булево
//
Процедура ПриСозданииНаСервере(Знач Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Если Спец_ОбщегоНазначенияКлиентСервер.ЭтоУправляемаяФорма(Форма) Тогда

		#Если Не ТолстыйКлиентОбычноеПриложение Тогда
	
			ДобавитьСлужебныеРеквизитыНаФорму(Форма, Истина);
			
			// ++ Спец_БСП.Спец_ЛогированиеОбъектов
			Спец_ЛогированиеОбъектов.ДобавитьНаФормуКнопкуОткрытияЛогов(Форма);
			// -- Спец_БСП.Спец_ЛогированиеОбъектов
	
		#КонецЕсли

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Если Не ТолстыйКлиентОбычноеПриложение Тогда

Процедура ДобавитьСлужебныеРеквизитыНаФорму(Знач Форма, Знач ЭтоУправляемаяФорма)
	
	// Вставим реквизиты ПолноеИмяМетаданных, ЭтоСсылочныйОбъект и ЭтоРегистр
	МассивРеквизитовДляДобавления = Новый Массив;
	
	Если Не Спец_РаботаСКоллекциямиКлиентСервер.ЕстьРеквизитОбъекта(Форма, "Спец_ПолноеИмяМетаданных") Тогда
		
		МассивРеквизитовДляДобавления.Добавить(Новый РеквизитФормы(
				"Спец_ПолноеИмяМетаданных",
				Спец_ОбщегоНазначенияКлиентСервер.ОписаниеТипаСтрока(256)));
		
	КонецЕсли;
	Если Не Спец_РаботаСКоллекциямиКлиентСервер.ЕстьРеквизитОбъекта(Форма, "Спец_ЭтоСсылочныйОбъект") Тогда
		
		МассивРеквизитовДляДобавления.Добавить(Новый РеквизитФормы(
				"Спец_ЭтоСсылочныйОбъект",
				Новый ОписаниеТипов("Булево")));
		
	КонецЕсли;
	Если Не Спец_РаботаСКоллекциямиКлиентСервер.ЕстьРеквизитОбъекта(Форма, "Спец_ЭтоРегистр") Тогда
		
		МассивРеквизитовДляДобавления.Добавить(Новый РеквизитФормы(
				"Спец_ЭтоРегистр",
				Новый ОписаниеТипов("Булево")));
		
	КонецЕсли;
	
	Если МассивРеквизитовДляДобавления.Количество() > 0 Тогда
		Форма.ИзменитьРеквизиты(МассивРеквизитовДляДобавления);
	КонецЕсли;
	
	// Заполним эти реквизиты
	Если Спец_РаботаСКоллекциямиКлиентСервер.ЕстьРеквизитОбъекта(Форма, "Объект")
		И Спец_РаботаСКоллекциямиКлиентСервер.ЕстьРеквизитОбъекта(Форма.Объект, "Ссылка") Тогда // Это ссылочный объект
		
		Форма.Спец_ПолноеИмяМетаданных = Форма.Объект.Ссылка.Метаданные().ПолноеИмя();
		Форма.Спец_ЭтоСсылочныйОбъект  = Истина;
		
	ИначеЕсли Спец_РаботаСКоллекциямиКлиентСервер.ЕстьРеквизитОбъекта(Форма, "Запись")
		И Спец_РаботаСКоллекциямиКлиентСервер.ЕстьРеквизитОбъекта(Форма.Запись, "ИсходныйКлючЗаписи") Тогда // Это регистр
		
		Форма.Спец_ПолноеИмяМетаданных = Форма.Запись.ИсходныйКлючЗаписи.Метаданные().ПолноеИмя();
		Форма.Спец_ЭтоРегистр          = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли

#КонецОбласти
