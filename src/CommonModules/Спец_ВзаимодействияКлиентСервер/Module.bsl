// @strict-types

//@skip-check method-too-many-params

#Если Не ВебКлиент Тогда

#Область ПрограммныйИнтерфейс

// Отправить E-Mail письмо.
// 
// Параметры:
//  МассивАдресовПолучателей - Массив из Строка, Строка - Массив адресов получателей
//  ТемаСообщения - Строка - Тема сообщения
//  ТекстСообщения - Строка - Текст сообщения
//  Отправитель - Строка - Отправитель
//  HTMLПисьмо - Булево - HTMLПисьмо
//  МассивВложений - Массив из Структура - Массив вложений:
//  	* Данные - ДвоичныеДанные - Двоичные данные вложения
//  	* Наименование - Строка - Наименование вложения
//  ТекстыОшибок - Строка - Тексты ошибок
//  СтруктураДополнительныхДанныхПоПисьму - Структура из КлючИЗначение - Структура дополнительных данных по письму:
//		* Ключ - Строка - Наименование дополнительного параметра
//		* Значение - Произвольный - Значение параметра
//
Процедура ОтправитьEMail(Знач МассивАдресовПолучателей, Знач ТемаСообщения, Знач ТекстСообщения, Знач Отправитель = "auto@farmani.ru",
		Знач HTMLПисьмо = Ложь, Знач МассивВложений = Неопределено, ТекстыОшибок = "", Знач СтруктураДополнительныхДанныхПоПисьму = Неопределено) Экспорт
	
	Спец_РаботаСКоллекциямиКлиентСервер.ЗначениеВМассив(МассивАдресовПолучателей);
	Если МассивАдресовПолучателей.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Кэш = Спец_ПолучитьКэш();
	#Если Клиент Тогда
		Спец_ОбщегоНазначенияКлиент.ВывестиСостояние("Подключение к почтовому серверу", 1, "Отправить EMail", БиблиотекаКартинок.Спец_ДлительнаяОперация, , Кэш);
	#КонецЕсли
	
	// ++ Спец_БСП.Спец_ЛогированиеОбъектов
	Спец_ЛогированиеОбъектовКлиентСервер.НачатьЗамерДляЛогирования("Отправка E-Mail письма", Кэш);
	// -- Спец_БСП.Спец_ЛогированиеОбъектов
	
	Если СтруктураДополнительныхДанныхПоПисьму = Неопределено Тогда
		СтруктураДополнительныхДанныхПоПисьму = Новый Структура;
	КонецЕсли;
	
	// ???
	//@skip-check unknown-method-property
	СтруктураПараметров = Спец_ВзаимодействияВызовСервераПовтИсп.ПолучитьСтруктуруПараметровОтправкиEMail();
	
	ПочтовыйПрофиль = Новый ИнтернетПочтовыйПрофиль;
	
	//ПочтовыйПрофиль.АдресСервераPOP3 = СтруктураПараметров.АдресСервераPOP3;
	ПочтовыйПрофиль.АдресСервераSMTP = СтруктураПараметров.АдресСервераSMTP;
	//ПочтовыйПрофиль.Пароль           = СтруктураПараметров.ПарольEMail;
	ПочтовыйПрофиль.ПарольSMTP = СтруктураПараметров.ПарольEMail;
	//ПочтовыйПрофиль.ПортPOP3         = СтруктураПараметров.ПортPOP3;
	ПочтовыйПрофиль.ПортSMTP = СтруктураПараметров.ПортSMTP;
	ПочтовыйПрофиль.ПользовательSMTP = СтруктураПараметров.ПользовательSMTP;
	//ПочтовыйПрофиль.Пользователь     = СтруктураПараметров.ПользовательEMail;
	ПочтовыйПрофиль.ПарольSMTP = СтруктураПараметров.ПарольSMTP;
	ПочтовыйПрофиль.Таймаут = СтруктураПараметров.ТаймаутОтправкиEMail;
	
	Соединение = Новый ИнтернетПочта;
	Попытка
		
		Соединение.Подключиться(ПочтовыйПрофиль);
		
	Исключение
		
		Спец_ОбщегоНазначенияКлиентСервер.ДобавитьОшибку(ИнформацияОбОшибке(),
				"Отправка E-Mail письма",
				ТекстыОшибок);
		
		Спец_ЗаписатьЛог("Отправка E-Mail письма", ПредопределенноеЗначение("Перечисление.Спец_УровниЛогирования.Ошибка"), ТекстыОшибок, Кэш);
		
		// ++ Спец_БСП.Спец_ЛогированиеОбъектов
		Спец_ЛогированиеОбъектовКлиентСервер.ЛогироватьЗавершениеЗамера("Отправка E-Mail письма", , Кэш);
		// -- Спец_БСП.Спец_ЛогированиеОбъектов
		
		Возврат;
		
	КонецПопытки;
	
	ИнтернетСообщение = Новый ИнтернетПочтовоеСообщение;
	
	ИнтернетСообщение.Кодировка = "UTF-8";
	ИнтернетСообщение.Отправитель = Отправитель;
	ИнтернетСообщение.Тема = ТемаСообщения;
	ИнтернетСообщение.УведомитьОПрочтении = Истина;
	
	Если МассивВложений <> Неопределено Тогда
		
		Спец_РаботаСКоллекциямиКлиентСервер.ЗначениеВМассив(МассивВложений);
		Для Каждого Вложение Из МассивВложений Цикл
			ИнтернетСообщение.Вложения.Добавить(Вложение.Данные, Вложение.Наименование);
		КонецЦикла;
		
	КонецЕсли;
	Для Каждого АдресПолучателя Из МассивАдресовПолучателей Цикл
		ИнтернетСообщение.Получатели.Добавить(АдресПолучателя);
	КонецЦикла;
	Если ЗначениеЗаполнено(СтруктураПараметров.ПодписьПисьмаEMail) Тогда
		
		ТекстСообщения = ТекстСообщения + Символы.ПС + Символы.ПС;
		Если HTMLПисьмо Тогда
			ТекстСообщения = ТекстСообщения + "<i>" + СтруктураПараметров.ПодписьПисьмаEMail + "</i>";
		Иначе
			ТекстСообщения = ТекстСообщения + СтруктураПараметров.ПодписьПисьмаEMail;
		КонецЕсли;
		
	КонецЕсли;
	
	Если HTMLПисьмо Тогда

		Если Спец_РаботаСКоллекциямиКлиентСервер.СвойствоСтруктуры(СтруктураДополнительныхДанныхПоПисьму, "ПропуститьЗаменуОтступов", Ложь) = Истина Тогда

			ТекстДляОтправки = ТекстСообщения;

		Иначе

			ТекстДляОтправки = СтрЗаменить(ТекстСообщения, Символы.ПС, "<br>");
			ТекстДляОтправки = СтрЗаменить(ТекстДляОтправки, Символы.Таб, "&nbsp;&nbsp;&nbsp;&nbsp;");

		КонецЕсли;
		
		// ???
		//@skip-check unknown-method-property
		ИнтернетСообщение.Тексты.Добавить(ТекстДляОтправки, ТипТекстаПочтовогоСообщения.HTML);

	Иначе
		
		// ???
		//@skip-check unknown-method-property
		ИнтернетСообщение.Тексты.Добавить(ТекстСообщения, ТипТекстаПочтовогоСообщения.ПростойТекст);

	КонецЕсли;

	Если СтруктураДополнительныхДанныхПоПисьму.Количество() > 0 Тогда
		ЗаполнитьЗначенияСвойств(ИнтернетСообщение, СтруктураДополнительныхДанныхПоПисьму);
	КонецЕсли;
	
	Попытка
		
		Соединение.Послать(ИнтернетСообщение);
		
	Исключение
		
		Спец_ОбщегоНазначенияКлиентСервер.ДобавитьОшибку(ИнформацияОбОшибке(),
				"Спец_Взаимодействия.ОтправитьEMail(...) -> Отправка сообщения",
				ТекстыОшибок);
		
		Спец_ЗаписатьЛог("Отправка E-Mail письма", ПредопределенноеЗначение("Перечисление.Спец_УровниЛогирования.Ошибка"), ТекстыОшибок, Кэш);
		
	КонецПопытки;
	
	Попытка
		
		Соединение.Отключиться();
		
	Исключение
		
		Спец_ОбщегоНазначенияКлиентСервер.ДобавитьОшибку(ИнформацияОбОшибке(),
				"Спец_Взаимодействия.ОтправитьEMail(...) -> Отключить почтовый профиль",
				ТекстыОшибок);
		
		Спец_ЗаписатьЛог("Отправка E-Mail письма", ПредопределенноеЗначение("Перечисление.Спец_УровниЛогирования.Предупреждение"), ТекстыОшибок, Кэш);
		
	КонецПопытки;
	
	// ++ Спец_БСП.Спец_ЛогированиеОбъектов
	Спец_ЛогированиеОбъектовКлиентСервер.ЛогироватьЗавершениеЗамера("Отправка E-Mail письма", , Кэш);
	// -- Спец_БСП.Спец_ЛогированиеОбъектов
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
