// @strict-types

//@skip-check common-module-type

#Область СлужебныйПрограммныйИнтерфейс

// Подключает компоненту BaseFunction.
// 
// Параметры:
//  КонстантноеИмяКомпоненты - Булево - Подключать компоненту с константным именем
//  ПринудительноеПодключение - Булево - Флаг того, что не нужно проверять работоспособность компоненты у пользователя
// 
// Возвращаемое значение:
//  Неопределено, ОбъектВнешнейКомпоненты - Компонента BaseFunction. Содержит следующие свойства:
// * ТекстПоследнейОшибки - Строка - Текст ошибок при выполнении последнего действия
// * ВерсияПриложения - Строка - Версия 1С приложения
// * ТипПриложения - Строка - Тип приложения, которое подключило данную компоненту
// * ПодключениеИзолированно - Булево - Флаг того, что компонента подключена изолированно
// * РазмерОчередиСобытий - Число - Количество событий в очереди для внешних событий
// * ВерсияКомпоненты - Строка - Текущая версия компоненты
// * ТихийРежим - Булево - Если ИСТИНА, то на клиенте модальные окна с ошибками НЕ будут показаны
//
Функция ПолучитьКомпонентуBaseFunction(Знач КонстантноеИмяКомпоненты = Истина, Знач ПринудительноеПодключение = Ложь) Экспорт
	
	Попытка
		
		// ???
		//@skip-check unknown-method-property
		Если Не ПринудительноеПодключение
			И Спец_КомпонентаДополнительныеФункцииВызовСервера.КомпонентаНеРаботаетУПользователя("ОбщийМакет.Спец_КомпонентаLykovExtFunctions") Тогда
			
			Возврат Неопределено;
			
		КонецЕсли;
		
		Возврат Спец_ОбщегоНазначенияКлиентСервер.ПодключитьВнешнююКомпонентуДляРаботы("ОбщийМакет.Спец_КомпонентаLykovExtFunctions",
				"l_BaseFunction",
				"BaseFunction",
				КонстантноеИмяКомпоненты,
				Истина);
		
	Исключение
		
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

// Подключает компоненту HTTPClient.
// 
// Параметры:
//  КонстантноеИмяКомпоненты - Булево - Подключать компоненту с константным именем
//  ПринудительноеПодключение - Булево - Флаг того, что не нужно проверять работоспособность компоненты у пользователя
// 
// Возвращаемое значение:
//  Неопределено, ОбъектВнешнейКомпоненты - Компонента BaseFunction. Содержит следующие свойства (описаны только базовые):
// * ТекстПоследнейОшибки - Строка - Текст ошибок при выполнении последнего действия
// * ВерсияПриложения - Строка - Версия 1С приложения
// * ТипПриложения - Строка - Тип приложения, которое подключило данную компоненту
// * ПодключениеИзолированно - Булево - Флаг того, что компонента подключена изолированно
// * РазмерОчередиСобытий - Число - Количество событий в очереди для внешних событий
// * ВерсияКомпоненты - Строка - Текущая версия компоненты
// * ТихийРежим - Булево - Если ИСТИНА, то на клиенте модальные окна с ошибками НЕ будут показаны
// * МаксимальноеКоличествоСоединенийВКэше - Число - https://curl.se/libcurl/c/CURLMOPT_MAXCONNECTS.html
// * МаксимальноеКоличествоПотоковДляHTTP2 - Число - https://curl.se/libcurl/c/CURLMOPT_MAX_CONCURRENT_STREAMS.html
// * МаксимальноеКоличествоСоединенийКОдномуХосту - Число - https://curl.se/libcurl/c/CURLMOPT_MAX_HOST_CONNECTIONS.html
// * МаксимальноеОбщееКоличествоСоединений - Число - https://curl.se/libcurl/c/CURLMOPT_MAX_TOTAL_CONNECTIONS.html
// * РежимОтладки - Булево - Флаг того, что нужно возвращать отладочную информацию по запросам.
// * ВозвращатьЗаголовки - Булево - Флаг того, что нужно возвращать заголовки по запросам.
// * ИспользоватьCookie - Булево - Флаг того, что нужно использовать Cookie в запросах.
// * ТаймаутМультиДескриптора - Число - https://curl.se/libcurl/c/curl_multi_wait.html
// * Username - Строка - https://curl.se/libcurl/c/CURLOPT_USERNAME.html
// * Password - Строка - https://curl.se/libcurl/c/CURLOPT_PASSWORD.html
// * Timeout_ms - Число - https://curl.se/libcurl/c/CURLOPT_TIMEOUT_MS.html
//
Функция ПолучитьКомпонентуHTTPClient(Знач КонстантноеИмяКомпоненты = Истина, Знач ПринудительноеПодключение = Ложь) Экспорт
	
	Попытка
		
		// ???
		//@skip-check unknown-method-property
		Если Не ПринудительноеПодключение
			И Спец_КомпонентаДополнительныеФункцииВызовСервера.КомпонентаНеРаботаетУПользователя("ОбщийМакет.Спец_КомпонентаLykovHTTPClient") Тогда
			
			Возврат Неопределено;
			
		КонецЕсли;
		
		Возврат Спец_ОбщегоНазначенияКлиентСервер.ПодключитьВнешнююКомпонентуДляРаботы("ОбщийМакет.Спец_КомпонентаLykovHTTPClient",
				"l_HTTPClient",
				"HttpClient",
				КонстантноеИмяКомпоненты,
				Истина);
		
	Исключение
		
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

// Подключает компоненту PostgreSQL.
// 
// Параметры:
//  КонстантноеИмяКомпоненты - Булево - Подключать компоненту с константным именем
//  ПринудительноеПодключение - Булево - Флаг того, что не нужно проверять работоспособность компоненты у пользователя
// 
// Возвращаемое значение:
//  Неопределено, ОбъектВнешнейКомпоненты - Компонента PostgreSQL. Содержит следующие свойства:
// * ТекстПоследнейОшибки - Строка - Текст ошибок при выполнении последнего действия
// * ВерсияПриложения - Строка - Версия 1С приложения
// * ТипПриложения - Строка - Тип приложения, которое подключило данную компоненту
// * ПодключениеИзолированно - Булево - Флаг того, что компонента подключена изолированно
// * РазмерОчередиСобытий - Число - Количество событий в очереди для внешних событий
// * ВерсияКомпоненты - Строка - Текущая версия компоненты
// * ТихийРежим - Булево - Если ИСТИНА, то на клиенте модальные окна с ошибками НЕ будут показаны
// * СоединениеУстановлено - Булево - Флаг того, что данная компонента подключена к БД
// * Хост - Строка - Имя хоста подключённой БД
// * НаименованиеБазы - Строка - Наименование базы подключённой БД
// * ИмяПользователя - Строка - Имя пользователя, от которого подключена БД
// * Порт - Число - Порт подключения БД
// * ИДПроцесса - Число - ИД процесса сервера БД
// * Сокет - Строка - Socket подключения к БД
// * ВерсияПротокола - Строка - Версия протокола подключения к БД
// * ВерсияСервера - Строка - Версия сервера БД
// * Кодировка - Строка - Кодировка подключения к БД
//
Функция ПолучитьКомпонентуPostgreSQL(Знач КонстантноеИмяКомпоненты = Истина, Знач ПринудительноеПодключение = Ложь) Экспорт
	
	Попытка
		
		// ???
		//@skip-check unknown-method-property
		Если Не ПринудительноеПодключение
			И Спец_КомпонентаДополнительныеФункцииВызовСервера.КомпонентаНеРаботаетУПользователя("ОбщийМакет.Спец_КомпонентаLykovPostgreSQL") Тогда
			
			Возврат Неопределено;
			
		КонецЕсли;
		
		Возврат Спец_ОбщегоНазначенияКлиентСервер.ПодключитьВнешнююКомпонентуДляРаботы("ОбщийМакет.Спец_КомпонентаLykovPostgreSQL",
				"l_PostgreSQL",
				"PostgreSQL",
				КонстантноеИмяКомпоненты,
				Истина);
		
	Исключение
		
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

// Подключает компоненту MySQL.
// 
// Параметры:
//  КонстантноеИмяКомпоненты - Булево - Подключать компоненту с константным именем
//  ПринудительноеПодключение - Булево - Флаг того, что не нужно проверять работоспособность компоненты у пользователя
// 
// Возвращаемое значение:
//  Неопределено, ОбъектВнешнейКомпоненты - Компонента MySQL. Содержит следующие свойства:
// * ТекстПоследнейОшибки - Строка - Текст ошибок при выполнении последнего действия
// * ВерсияПриложения - Строка - Версия 1С приложения
// * ТипПриложения - Строка - Тип приложения, которое подключило данную компоненту
// * ПодключениеИзолированно - Булево - Флаг того, что компонента подключена изолированно
// * РазмерОчередиСобытий - Число - Количество событий в очереди для внешних событий
// * ВерсияКомпоненты - Строка - Текущая версия компоненты
// * ТихийРежим - Булево - Если ИСТИНА, то на клиенте модальные окна с ошибками НЕ будут показаны
// * ИнформацияОСоединении - Строка - Информация по текущему подключению к БД
//
Функция ПолучитьКомпонентуMySQL(Знач КонстантноеИмяКомпоненты = Истина, Знач ПринудительноеПодключение = Ложь) Экспорт
	
	Попытка
		
		// ???
		//@skip-check unknown-method-property
		Если Не ПринудительноеПодключение
			И Спец_КомпонентаДополнительныеФункцииВызовСервера.КомпонентаНеРаботаетУПользователя("ОбщийМакет.Спец_КомпонентаLykovMySQL") Тогда
			
			Возврат Неопределено;
			
		КонецЕсли;
		
		Возврат Спец_ОбщегоНазначенияКлиентСервер.ПодключитьВнешнююКомпонентуДляРаботы("ОбщийМакет.Спец_КомпонентаLykovMySQL",
				"l_MySQLConn",
				"MySQLConn",
				КонстантноеИмяКомпоненты,
				Истина);
		
	Исключение
		
		Возврат Неопределено;
		
	КонецПопытки;
	
КонецФункции

// Соответствие версий моих компонент. Заполняется руками (чтобы при запуске не терять время на подключение каждой компоненты).
// 
// Возвращаемое значение:
//  ФиксированноеСоответствие из КлючИЗначение:
//		* Ключ - Строка
//		* Значение - см. НоваяСтруктураВерсииМоейКомпоненты
//
Функция ПолучитьСоответствиеВерсийМоихКомпонент() Экспорт
	
	СоответствиеРезультат = Новый Соответствие();
	
	СоответствиеРезультат["ОбщийМакет.Спец_КомпонентаLykovExtFunctions"] = НоваяСтруктураВерсииМоейКомпоненты("1.0.0.17", "ПолучитьКомпонентуBaseFunction");
	СоответствиеРезультат["ОбщийМакет.Спец_КомпонентаLykovHTTPClient"]   = НоваяСтруктураВерсииМоейКомпоненты("1.0.0.17", "ПолучитьКомпонентуHTTPClient");
	СоответствиеРезультат["ОбщийМакет.Спец_КомпонентаLykovMySQL"]        = НоваяСтруктураВерсииМоейКомпоненты("1.0.0.17", "ПолучитьКомпонентуMySQL");
	СоответствиеРезультат["ОбщийМакет.Спец_КомпонентаLykovPostgreSQL"]   = НоваяСтруктураВерсииМоейКомпоненты("1.0.0.17", "ПолучитьКомпонентуPostgreSQL");
	
	Возврат Спец_РаботаСКоллекциямиКлиентСервер.СкопироватьСоответствие(СоответствиеРезультат, Истина);
	
КонецФункции

// Массив имён моих компонент.
// 
// Возвращаемое значение:
//  ФиксированныйМассив из Строка - Массив имён моих компонент
//
Функция ПолучитьМассивИменМоихКомпонент() Экспорт
	
	МассивРезультат = Новый Массив(); // Массив из Строка
	Для Каждого КлючЗначение Из Спец_КомпонентаДополнительныеФункцииКлиентСерверПовтИсп.ПолучитьСоответствиеВерсийМоихКомпонент() Цикл
		МассивРезультат.Добавить(КлючЗначение.Ключ);
	КонецЦикла;
	
	Возврат Новый ФиксированныйМассив(МассивРезультат);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область Типизация

// Параметры:
//  НомерВерсии - Строка
//  НаименованиеФункцииДляПодключения - Строка
// 
// Возвращаемое значение:
//  Структура:
//		* НомерВерсии - Строка
//		* НаименованиеФункцииДляПодключения - Строка
//
Функция НоваяСтруктураВерсииМоейКомпоненты(Знач НомерВерсии, Знач НаименованиеФункцииДляПодключения)
	
	Возврат Новый Структура("НомерВерсии, НаименованиеФункцииДляПодключения",
			НомерВерсии, НаименованиеФункцииДляПодключения);
	
КонецФункции

#КонецОбласти

#КонецОбласти
