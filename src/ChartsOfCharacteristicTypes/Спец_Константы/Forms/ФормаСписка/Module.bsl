
#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
	
	СоответствиеСтрокДляЗаполнения = Новый Соответствие;
	МассивДляПолученияЗначений     = Новый Массив;
	
	Для Каждого СтрокаСписка Из Строки Цикл
		
		ДанныеСтроки = СтрокаСписка.Значение.Данные;
		Если ТипЗнч(ДанныеСтроки.ТекущееЗначение) = Тип("СправочникСсылка.Спец_МассивЗначенийКонстант")
			И ЗначениеЗаполнено(ДанныеСтроки.ТекущееЗначение) Тогда
			
			МассивДляПолученияЗначений.Добавить(ДанныеСтроки.ТекущееЗначение);
			СоответствиеСтрокДляЗаполнения[СтрокаСписка.Значение.Данные.ТекущееЗначение] = ДанныеСтроки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если МассивДляПолученияЗначений.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СоответствиеРеквизитов = Спец_ОбщегоНазначения.ЗначениеРеквизитаОбъектов(МассивДляПолученияЗначений, "ЗначенияКонстанты");
	Для Каждого КлючЗначение Из СоответствиеСтрокДляЗаполнения Цикл
		
		Если СоответствиеРеквизитов[КлючЗначение.Ключ] = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ТаблицаЗначений = СоответствиеРеквизитов[КлючЗначение.Ключ].Выгрузить();
		ТаблицаЗначений.Сортировать("НомерСтроки");
		
		КлючЗначение.Значение["ТекущееЗначение"] = СтрСоединить(ТаблицаЗначений.ВыгрузитьКолонку("Значение"), "; ");
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
