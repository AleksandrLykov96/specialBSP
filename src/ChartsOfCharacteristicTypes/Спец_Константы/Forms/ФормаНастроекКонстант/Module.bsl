
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ЗначениеЗаполнено(Параметры.КонстантаДляНастройки) Тогда
		КонстантаДляНастройки = Параметры.КонстантаДляНастройки;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(КонстантаДляНастройки) Тогда
		КонстантаДляНастройкиПриИзменении(Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура КонстантаДляНастройкиПриИзменении(Элемент)
	
	ЗначениеДляПодставноки = Спец_ПолучитьКонстанту(КонстантаДляНастройки, Неопределено, , , Ложь);
	Если Спец_РаботаСКоллекциямиКлиентСервер.ЭтоМассив(ЗначениеДляПодставноки) Тогда
		ЗначениеДляПодставноки = Спец_ОбщегоНазначенияВызовСервера.ПолучитьМассовоеЗначениеПоПВХ(КонстантаДляНастройки);
	КонецЕсли;
	
	ТекущееЗначение = ЗначениеДляПодставноки;
	УстановитьВидимостьДоступность(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаписать(Команда)
	
	Если Не ЗначениеЗаполнено(КонстантаДляНастройки) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ТекущееЗначение) = Тип("СправочникСсылка.Спец_МассивЗначенийКонстант") Тогда
		Спец_УстановитьКонстанту(КонстантаДляНастройки, МассивЗначенийКонстанты(ТекущееЗначение));
	Иначе
		Спец_УстановитьКонстанту(КонстантаДляНастройки, ТекущееЗначение);
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьВидимостьДоступность(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ФормаЗаписать.Доступность = ЗначениеЗаполнено(Форма.КонстантаДляНастройки);
	Элементы.ТекущееЗначение.ТолькоПросмотр = Не ЗначениеЗаполнено(Форма.КонстантаДляНастройки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МассивЗначенийКонстанты(Знач СсылкаНаМассивЗначений)
	
	Возврат Спец_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаМассивЗначений, "ЗначенияКонстанты").Выгрузить().ВыгрузитьКолонку("Значение");
	
КонецФункции

#КонецОбласти
