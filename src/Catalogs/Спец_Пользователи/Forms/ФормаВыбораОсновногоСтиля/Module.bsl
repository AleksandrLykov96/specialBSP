
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Пользователь", Пользователь);
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		Пользователь = ПараметрыСеанса.Спец_ТекущийПользователь;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Пользователь) Тогда
		ВызватьИсключение "Не указан пользователь для настройки!";
	КонецЕсли;
	
	Элементы.СтильКонфигурации.СписокВыбора.Очистить();
	Для Каждого МетаданныеСтиль Из Метаданные.Стили Цикл
		Элементы.СтильКонфигурации.СписокВыбора.Добавить(МетаданныеСтиль.Имя, МетаданныеСтиль.Представление());
	КонецЦикла;
	
	СтильКонфигурации = Спец_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Пользователь, "ОсновнойСтиль");
	УстановитьКартинкуСтиля();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтильКонфигурацииПриИзменении(Элемент)

	УстановитьКартинкуСтиля();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаписать(Команда)
	
	Если Не ЗаписатьОсновнойСтильНаСервере() Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещенияПослеЗакрытияПредупреждения = Новый ОписаниеОповещения("ПослеЗакрытияПредупреждения", ЭтотОбъект);
	ПоказатьПредупреждение(ОписаниеОповещенияПослеЗакрытияПредупреждения,
			"Для установки стиля в программе необходимо перезайти в 1С",
			120,
			"Внимание");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьКартинкуСтиля()
	
	Если ПустаяСтрока(СтильКонфигурации) Тогда
		НаименованиеКартинкиРезультат = "Спец_ОсновнойСтиль";
	Иначе
		НаименованиеКартинкиРезультат = СтильКонфигурации + "Стиль";
	КонецЕсли;
	
	Если Метаданные.ОбщиеКартинки.Найти(НаименованиеКартинкиРезультат) <> Неопределено Тогда
		КартинкаСтиль = БиблиотекаКартинок[НаименованиеКартинкиРезультат];
	Иначе
		КартинкаСтиль = Неопределено;
	КонецЕсли;
	
	Элементы.КартинкаСтиль.Видимость = КартинкаСтиль <> Неопределено;
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьОсновнойСтильНаСервере()
	
	Кэш = Спец_ПолучитьКэш();
	
	СправочникОбъект = Спец_ОбщегоНазначения.ПолучитьОбъектСБлокированием(Пользователь, Кэш);
	Если СправочникОбъект <> Неопределено Тогда
		
		СправочникОбъект.ОсновнойСтиль = СтильКонфигурации;
		
		МассивРеквизитов = Спец_РаботаСКоллекциямиКлиентСервер.ЗначениеВМассиве("ОсновнойСтиль");
		СтруктураРеквизитов = Новый Структура("МассивРеквизитов", МассивРеквизитов);
		
		Возврат Спец_ОбщегоНазначения.ЗаписатьОбъект(СправочникОбъект, , , , Кэш, , СтруктураРеквизитов);
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПослеЗакрытияПредупреждения(Знач ДополнительныеПараметры) Экспорт
	
	Если Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
