
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда     
		Возврат;
	КонецЕсли;
	
	Спец_РаботаСФормами.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	Элементы.ПараметрыРаботыСКомпонентамиНаименованиеКомпоненты.СписокВыбора.Очистить();
	Для Каждого НаименованиеКомпоненты Из Спец_КомпонентаДополнительныеФункцииКлиентСерверПовтИсп.ПолучитьМассивИменМоихКомпонент() Цикл
		Элементы.ПараметрыРаботыСКомпонентамиНаименованиеКомпоненты.СписокВыбора.Добавить(НаименованиеКомпоненты);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Спец_РаботаСФормамиКлиент.ПриОткрытии(ЭтотОбъект, Отказ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Спец_РаботаСФормамиКлиент.ПриЗакрытии(ЭтотОбъект, ЗавершениеРаботы);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбратьОсновнойСтиль(Команда)
	
	ОписаниеОповещенияПослеВопроса = Новый ОписаниеОповещения("ПослеЗаписиОбъектаЕслиТребуется", ЭтотОбъект);
	Если Модифицированность Тогда
		
		ПоказатьВопрос(ОписаниеОповещенияПослеВопроса,
				"Объект должен быть записан перед выбором стиля. Записать?",
				РежимДиалогаВопрос.ДаНет);
		
	Иначе
		
		ВыполнитьОбработкуОповещения(ОписаниеОповещенияПослеВопроса, КодВозвратаДиалога.Пропустить);
		
	КонецЕсли;
	
КонецПроцедуры

// ++ Спец_БСП.Спец_ЛогированиеОбъектов
&НаКлиенте
Процедура Подключаемый_ОткрытьЛогиПоОбъекту(Команда)
	
	Спец_ЛогированиеОбъектовКлиент.ОткрытьЛогиПоОбъекту(ЭтотОбъект);
	
КонецПроцедуры
// -- Спец_БСП.Спец_ЛогированиеОбъектов

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеЗаписиОбъектаЕслиТребуется(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Пропустить
			Или (Результат = КодВозвратаДиалога.Да И Записать()) Тогда
		
		ОписаниеОповещенияПослеЗакрытия = Новый ОписаниеОповещения("ПослеВыбораОсновногоСтиля", ЭтотОбъект);
		
		ПараметрыОткрытияФормы = Новый Структура("Пользователь", Объект.Ссылка);
		ОткрытьФорму("Справочник.Спец_Пользователи.Форма.ФормаВыбораОсновногоСтиля",
				ПараметрыОткрытияФормы,
				ЭтотОбъект,
				, , ,
				ОписаниеОповещенияПослеЗакрытия);
				
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораОсновногоСтиля(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Прочитать();
	
КонецПроцедуры

#КонецОбласти
