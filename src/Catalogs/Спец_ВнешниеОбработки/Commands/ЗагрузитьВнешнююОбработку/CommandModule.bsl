
// @strict-types

#Область ОбработчикиСобытий

// Параметры:
//  ПараметрКоманды - СправочникСсылка.Спец_ВнешниеОбработки
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если Не ЗначениеЗаполнено(ПараметрКоманды) Тогда
		ВызватьИсключение "Не указан элемент для записи!";
	КонецЕсли;
	
	// ++ Спец_БСП.Версия1С_18_Ниже
	////ВызватьИсключение "Недоступно!";
	// -- Спец_БСП.Версия1С_18_Ниже
	
	// ++ Спец_БСП.Версия1С_18_ВышеИлиРавно
	Спец_ОбщегоНазначенияКлиент.ВывестиСостояние("Идёт загрузка файла на сервер...");
	ЗагрузитьФайлОбработкиАсинх(ПараметрКоманды, ПараметрыВыполненияКоманды.Источник.УникальныйИдентификатор);
	// -- Спец_БСП.Версия1С_18_ВышеИлиРавно
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// ++ Спец_БСП.Версия1С_18_ВышеИлиРавно
&НаКлиенте
Асинх Процедура ЗагрузитьФайлОбработкиАсинх(Знач ВнешняяОбработка, Знач ИдентификаторФормы)
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов("Выберите файл для записи во внешнюю обработку",
			Ложь,
			СтрШаблон("Внешние отчеты и обработки (*.%1, *.%2)|*.%1;*.%2|Внешние отчеты (*.%1)|*.%1|Внешние обработки (*.%2)|*.%2", "erf", "epf"));
	
	МассивФайловРезультат = Ждать Спец_ОбщегоНазначенияКлиент.ЗагрузитьФайлыНаСерверАсинх(ПараметрыДиалога, ИдентификаторФормы); // Массив из ОписаниеПомещенногоФайла
	Если МассивФайловРезультат.Количество() > 0 Тогда
		ОбработкаКоманды_ЗаписатьИзмененияНаСервере(МассивФайловРезультат.Получить(0).Адрес, ВнешняяОбработка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаКоманды_ЗаписатьИзмененияНаСервере(Знач Адрес, Знач СправочникСсылка)
	
	СправочникОбъект = СправочникСсылка.ПолучитьОбъект();
	СправочникОбъект.ХранилищеВнешнейОбработки = Новый ХранилищеЗначения(ПолучитьИзВременногоХранилища(Адрес));
	
	СправочникОбъект.Записать();
	
КонецПроцедуры
// -- Спец_БСП.Версия1С_15_ВышеИлиРавно

#КонецОбласти
