
// @strict-types

#Область ОбработчикиСобытий

// Параметры:
//  ПараметрКоманды - СправочникСсылка.Спец_ВнешниеОбработки
//  ПараметрыВыполненияКоманды - ПараметрыВыполненияКоманды
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Если Не ЗначениеЗаполнено(ПараметрКоманды) Тогда
		ВызватьИсключение "Не указан элемент для получения данных из обработки!";
	КонецЕсли;
	
	// ++ Спец_БСП.Версия1С_18_Ниже
	////ВызватьИсключение "Недоступно!";
	// -- Спец_БСП.Версия1С_18_Ниже
	
	// ++ Спец_БСП.Версия1С_18_ВышеИлиРавно
	Спец_ОбщегоНазначенияКлиент.ВывестиСостояние("Идёт сохранение обработки...");
	СохранитьОбработку(ПараметрКоманды);
	// -- Спец_БСП.Версия1С_18_ВышеИлиРавно
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// ++ Спец_БСП.Версия1С_18_ВышеИлиРавно
&НаКлиенте
Асинх Процедура СохранитьОбработку(Знач ВнешняяОбработка)
	
	//@skip-check transfer-object-between-client-server
	ОписаниеФайла = ОписаниеПередаваемогоФайлаДляЗагрузкиОбработки(ВнешняяОбработка);
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПолученияФайлов("Куда сохранить обработку", Ложь);
	Спец_ОбщегоНазначенияКлиент.ЗагрузитьФайлыССервераАсинх(ПараметрыДиалога, ОписаниеФайла);
	
КонецПроцедуры

//@skip-check property-return-type
//@skip-check dynamic-access-method-not-found
&НаСервере
Функция ОписаниеПередаваемогоФайлаДляЗагрузкиОбработки(Знач СправочникСсылка)
	
	РеквизитыОбработки = Спец_ОбщегоНазначения.ЗначенияРеквизитовОбъекта(СправочникСсылка, "Наименование, ХранилищеВнешнейОбработки");
	
	Возврат Новый ОписаниеПередаваемогоФайла(РеквизитыОбработки.Наименование + ".epf",
			ПоместитьВоВременноеХранилище(РеквизитыОбработки.ХранилищеВнешнейОбработки.Получить()));
	
КонецФункции
// -- Спец_БСП.Версия1С_18_ВышеИлиРавно

#КонецОбласти
