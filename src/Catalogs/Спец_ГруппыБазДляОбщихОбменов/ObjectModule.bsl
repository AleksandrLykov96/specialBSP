
// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		ОбменВключен = Ложь;
	КонецЕсли;
	
	ОбщиеБазы.Сортировать("База");
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// Проставляем "Обмен включён"
	Кэш = Спец_ПолучитьКэш();
	Для Каждого СтрокаБаза Из ОбщиеБазы Цикл
		ПроставитьОбменВключенУБазы(СтрокаБаза.База, Отказ, Кэш);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  База - СправочникСсылка.Спец_Базы
//  Отказ - Булево
//  Кэш - см. Спец_КлиентСерверГлобальный.Спец_ПолучитьКэш
Процедура ПроставитьОбменВключенУБазы(Знач База, Отказ, Знач Кэш)
	
	СправочникОбъект = Спец_ОбщегоНазначения.ПолучитьОбъектСБлокированием(База, Кэш); // СправочникОбъект.Спец_Базы
	Если СправочникОбъект <> Неопределено И СправочникОбъект.ВключитьОбмен <> ОбменВключен Тогда

		СправочникОбъект.ВключитьОбмен = ОбменВключен;
		Спец_ОбщегоНазначения.ЗаписатьОбъект(СправочникОбъект, , , Отказ, Кэш, , , Ложь);

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
