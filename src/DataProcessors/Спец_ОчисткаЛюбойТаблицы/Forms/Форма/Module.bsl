
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Кэш = Спец_ПолучитьКэш();
	ТипБазы = Спец_ОбщегоНазначенияПовтИсп.СтруктураПараметровБазы().ТипБазыДанных;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СтруктураТаблицБазыДанных.Метаданные КАК Метаданные,
	|	СтруктураТаблицБазыДанных.НаименованиеВ1С КАК НаименованиеВ1С
	|ИЗ
	|	Справочник.Спец_СтруктураТаблицБазыДанных КАК СтруктураТаблицБазыДанных
	|ГДЕ
	|	СтруктураТаблицБазыДанных.Метаданные <> """"
	|	И СтруктураТаблицБазыДанных.НаименованиеВ1С <> """"";
	
	ВыборкаРезультат = Запрос.Выполнить().Выбрать();
	Пока ВыборкаРезультат.Следующий() Цикл
		
		МетаданныеОбъекта = Метаданные.НайтиПоПолномуИмени(ВыборкаРезультат.Метаданные);
		Если МетаданныеОбъекта = Неопределено Или Спец_ОбщегоНазначения.ЭтоПеречисление(МетаданныеОбъекта, Кэш) Тогда
			Продолжить;
		КонецЕсли;
		
		РодительМетаданных = МетаданныеОбъекта.Родитель();
		Если СтрЗаканчиваетсяНа(ВыборкаРезультат.НаименованиеВ1С, ".Изменения") Тогда
			
			СтруктураПоМетаданным = Спец_ОбщегоНазначения.ПолучитьСтруктуруИмениТипаДанныхОбъекта(МетаданныеОбъекта, Кэш);
			Элементы.ИмяТаблицы.СписокВыбора.Добавить(ВыборкаРезультат.НаименованиеВ1С, СтрШаблон("%1: %2 - %3",
				СтруктураПоМетаданным.ТипОбъектаЕдинственноеЧисло,
				МетаданныеОбъекта.Представление(),
				"Изменения планов обменов"));
			
		ИначеЕсли ТипЗнч(РодительМетаданных) = Тип("ОбъектМетаданныхКонфигурация") Тогда
			
			СтруктураПоМетаданным = Спец_ОбщегоНазначения.ПолучитьСтруктуруИмениТипаДанныхОбъекта(МетаданныеОбъекта, Кэш);
			Элементы.ИмяТаблицы.СписокВыбора.Добавить(ВыборкаРезультат.НаименованиеВ1С, СтрШаблон("%1: %2",
					СтруктураПоМетаданным.ТипОбъектаЕдинственноеЧисло,
					МетаданныеОбъекта.Представление()));
			
		Иначе
			
			СтруктураПоМетаданным = Спец_ОбщегоНазначения.ПолучитьСтруктуруИмениТипаДанныхОбъекта(РодительМетаданных, Кэш);
			Элементы.ИмяТаблицы.СписокВыбора.Добавить(ВыборкаРезультат.НаименованиеВ1С, СтрШаблон("%1: %2 - %3",
				СтруктураПоМетаданным.ТипОбъектаЕдинственноеЧисло,
				РодительМетаданных.Представление(),
				МетаданныеОбъекта.Представление()));
			
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы.ИмяТаблицы.СписокВыбора.СортироватьПоПредставлению();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяТаблицыАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Спец_РаботаСФормамиКлиент.АвтоПодборТекстаИзСпискаВыбораСтрок(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОчистить(Команда)
	
	Если ПустаяСтрока(ИмяТаблицы) Тогда
		Возврат;
	КонецЕсли;
	
	// ++ Спец_БСП.Версия1С_18_ВышеИлиРавно
	ОчисткаТаблицыАсинх();
	// -- Спец_БСП.Версия1С_18_ВышеИлиРавно
	
	// ++ Спец_БСП.Версия1С_18_Ниже
	////ОчисткаТаблицы();
	// -- Спец_БСП.Версия1С_18_Ниже
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// ++ Спец_БСП.Версия1С_18_ВышеИлиРавно
&НаКлиенте
Асинх Процедура ОчисткаТаблицыАсинх()
	
	Кэш = Спец_ПолучитьКэш();
	Спец_ОбщегоНазначенияКлиент.ВывестиСостояниеАсинх("Очистка таблицы " + ИмяТаблицы);
	
	Элементы.ФормаОчистить.Видимость = Ложь;
	
	Ждать Спец_КомпонентаДополнительныеФункцииКлиент.НачатьЗамерАсинх("ОчисткаТаблицы", Кэш);
	
	УспешноОчищено = Ждать Спец_РаботаСБазойДанныхНапрямуюКлиент.УдалитьДанныеПоОбъекту1СВБазеДанныхАсинх(Новый Массив(), ИмяТаблицы, , Кэш);
	Если Не УспешноОчищено Тогда
		
		Элементы.ФормаОчистить.Видимость = Истина;
		ВызватьИсключение "Не удалось! Возможно, тип базы данных не поддерживается.";
		
	КонецЕсли;
	ВремяВыполнения = Строка(Ждать Спец_КомпонентаДополнительныеФункцииКлиент.ЗавершитьЗамерАсинх("ОчисткаТаблицы", Кэш)) + " сек.";
	
	Элементы.ФормаОчистить.Видимость = Истина;
	
КонецПроцедуры
// -- Спец_БСП.Версия1С_18_ВышеИлиРавно

// ++ Спец_БСП.Версия1С_18_Ниже
////&НаКлиенте
////Процедура ОчисткаТаблицы()
////	
////	Кэш = Спец_ПолучитьКэш();
////	
////	Спец_КомпонентаДополнительныеФункцииКлиентСервер.НачатьЗамер("ОчисткаТаблицы", Кэш);
////	Если Не Спец_РаботаСБазойДанныхНапрямуюКлиентСервер.УдалитьДанныеПоОбъекту1СВБазеДанных(Новый Массив(), ИмяТаблицы, , Кэш) Тогда
////		ВызватьИсключение "Не удалось! Возможно, тип базы данных не поддерживается.";
////	КонецЕсли;
////	ВремяВыполнения = Строка(Спец_КомпонентаДополнительныеФункцииКлиентСервер.ЗавершитьЗамер("ОчисткаТаблицы", Кэш)) + " сек.";
////	
////КонецПроцедуры
// -- Спец_БСП.Версия1С_18_Ниже

#КонецОбласти
