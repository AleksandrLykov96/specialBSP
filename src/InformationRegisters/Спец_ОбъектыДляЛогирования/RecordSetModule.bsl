// @strict-types

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Кэш = Спец_ПолучитьКэш();
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		СтруктураМетаданных = Спец_ОбщегоНазначения.ПолучитьСтруктуруИмениТипаДанныхОбъекта(Запись.ИмяМетаданных, Кэш);
		Запись.ЭтоПользователь = СтруктураМетаданных.МетаданныеОбъекта = Неопределено;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Отбор.ИмяМетаданных.Использование Тогда
		
		ИмяМетаданных = Отбор.ИмяМетаданных.Значение; // Строка
		УстановитьНастройкуИсторииДанных(ИмяМетаданных);
		
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Параметры:
//  ИмяМетаданных - Строка
Процедура УстановитьНастройкуИсторииДанных(Знач ИмяМетаданных)

	Если ПустаяСтрока(ИмяМетаданных) Тогда
		Возврат;
	КонецЕсли;

	Кэш = Спец_ПолучитьКэш();

	СтруктураМетаданных = Спец_ОбщегоНазначения.ПолучитьСтруктуруИмениТипаДанныхОбъекта(ИмяМетаданных, Кэш);
	Если СтруктураМетаданных.МетаданныеОбъекта = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Количество() > 0 Тогда
		Спец_ЛогированиеОбъектов.ВключитьТиповойМеханизмИсторииПоОбъектуМетаданных(СтруктураМетаданных.МетаданныеОбъекта, Кэш);
	Иначе
		ИсторияДанных.УстановитьНастройки(СтруктураМетаданных.МетаданныеОбъекта, Неопределено);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
